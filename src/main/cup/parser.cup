package com.clysman.compiler;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;

parser code {:
    private Stack<Map<String, SymbolInfo>> variableScopeStack = new Stack<>();
    private Stack<Map<String, FunctionInfo>> functionScopeStack = new Stack<>();
    private String currentFunction = null;
    private String currentFunctionReturnType = null;

    public Map<String, SymbolInfo> currentScope() {
        if (variableScopeStack.isEmpty()) {
            Map<String, SymbolInfo> symbolTable = new HashMap<>();
            variableScopeStack.push(symbolTable);
        }
        return variableScopeStack.peek();
    }

    public Map<String, FunctionInfo> currentFunctionScope() {
        if (functionScopeStack.isEmpty()) {
            Map<String, FunctionInfo> functionTable = new HashMap<>();
            functionScopeStack.push(functionTable);
        }
        return functionScopeStack.peek();
    }

    public void enterScope() {
        variableScopeStack.push(new HashMap<String, SymbolInfo>());
        functionScopeStack.push(new HashMap<String, FunctionInfo>());
    }

    public void exitScope() {
        if (!variableScopeStack.isEmpty()) {
            variableScopeStack.pop();
        }
        if (!functionScopeStack.isEmpty()) {
            functionScopeStack.pop();
        }
    }

    public void declareVariable(String name, Object type) {
        Map<String, SymbolInfo> symbolTable = currentScope();
        if (symbolTable.containsKey(name)) {
            fatal_error(ErrorType.SEMANTICAL, "Variável '" + name + "' já foi declarada.");
        }
        symbolTable.put(name, new SymbolInfo(name, type));
    }

    public SymbolInfo lookupVariable(String name) {
        for (int i = variableScopeStack.size() - 1; i >= 0; i--) {
            Map<String, SymbolInfo> symbolTable = variableScopeStack.get(i);
            if (symbolTable.containsKey(name)) {
                return symbolTable.get(name);
            }
        }
        return null;
    }

    public void declareFunction(String name, String returnType, List<String> paramTypes) {
        Map<String, FunctionInfo> functionTable = currentFunctionScope();
        if (functionTable.containsKey(name)) {
            fatal_error(ErrorType.SEMANTICAL, "Função '" + name + "' já foi declarada neste escopo.");
        }
        functionTable.put(name, new FunctionInfo(name, returnType, paramTypes));
    }

    public FunctionInfo lookupFunction(String name) {
        // Busca função em todos os escopos, do mais interno para o mais externo
        for (int i = functionScopeStack.size() - 1; i >= 0; i--) {
            Map<String, FunctionInfo> functionTable = functionScopeStack.get(i);
            if (functionTable.containsKey(name)) {
                return functionTable.get(name);
            }
        }
        return null;
    }

    private scanner scanner;

    public parser(scanner scanner) {
        this.scanner = scanner;
    }

    public enum ErrorType {
        SEMANTICAL("SemanticalError"),
        TYPE("TypeError"),
        SYNTAX("SyntaxError");

        private final String description;

        ErrorType(String description) {
            this.description = description;
        }

        public String getDescription() {
            return description;
        }
    }

    public void syntax_error(Symbol cur_token) {
        fatal_error(ErrorType.SYNTAX, "Token inesperado '" + cur_token.value + "'");
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        fatal_error(ErrorType.SYNTAX, "Erro de sintaxe não recuperável. Compilação interrompida.");
    }

    private void fatal_error(ErrorType errorType, String message, int line, int column) {
        System.err.println("[" + errorType.getDescription() + " - Linha " + line +
                          ", Coluna " + column + "] " + message);
        System.exit(1);
    }

    private void fatal_error(ErrorType errorType, String message) {
        System.err.println("["+ errorType.getDescription() + "] " + message);
        System.exit(1);
    }

    public class SymbolInfo {
        public String name;
        public Object type;

        public SymbolInfo(String name, Object type) {
            this.name = name;
            this.type = type;
        }
    }

    public class FunctionInfo {
        public String name;
        public String returnType;
        public List<String> parameterTypes;

        public FunctionInfo(String name, String returnType, List<String> parameterTypes) {
            this.name = name;
            this.returnType = returnType;
            this.parameterTypes = parameterTypes != null ? parameterTypes : new ArrayList<>();
        }
    }

    class ExprResult {
        public Object type;
        public String representation;

        ExprResult(String type, String representation) {
            this.type = type;
            this.representation = representation;
        }

        public String toString() {
            return this.representation;
        }
    }

    class ParameterList {
        public List<String> types;
        public List<String> names;

        public ParameterList() {
            this.types = new ArrayList<>();
            this.names = new ArrayList<>();
        }

        public void addParameter(String type, String name) {
            this.types.add(type);
            this.names.add(name);
        }
    }
:}

scan with {: return scanner.next_token(); :};

terminal IF, ELSE, WHILE, FOR, RETURN;
terminal INT_TYPE, FLOAT_TYPE, BOOL_TYPE, STRING_TYPE, VOID;
terminal TRUE, FALSE;
terminal FUNCTION;
terminal BREAK, CONTINUE;

terminal PLUS, MINUS, TIMES, DIVIDE, MOD;
terminal EQ, NE, LT, LE, GT, GE;
terminal AND, OR, NOT;
terminal ASSIGN, PLUS_ASSIGN, MINUS_ASSIGN, TIMES_ASSIGN, DIVIDE_ASSIGN;

terminal SEMICOLON, COMMA;
terminal LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET;

terminal String IDENTIFIER;
terminal Integer INT_LITERAL;
terminal Double FLOAT_LITERAL;
terminal String STRING_LITERAL;

non terminal program;
non terminal statement_list;
non terminal statement;
non terminal declaration;
non terminal assignment;
non terminal function_statement;
non terminal parameter_list;
non terminal parameter;
non terminal if_statement;
non terminal while_statement;
non terminal for_statement;
non terminal return_statement;
non terminal block;
non terminal unscoped_block;
non terminal expression;
non terminal logical_or_expr;
non terminal logical_and_expr;
non terminal equality_expr;
non terminal relational_expr;
non terminal additive_expr;
non terminal multiplicative_expr;
non terminal unary_expr;
non terminal primary_expr;
non terminal function_call;
non terminal argument_list;
non terminal type;

precedence left OR;
precedence left AND;
precedence left EQ, NE;
precedence left LT, LE, GT, GE;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, MOD;
precedence right NOT;

start with program;

program ::= statement_list:sl
    | /* programa vazio */
;

statement_list ::= statement_list:sl statement:s
    | statement:s
;

statement ::= declaration:d
    | assignment:a
    | function_statement:fc
    | if_statement:i
    | while_statement:w
    | for_statement:f
    | return_statement:r
    | function_call:fc SEMICOLON
;

declaration ::= type:t IDENTIFIER:id SEMICOLON
        {:
            declareVariable(id, t);
        :}
    | type:t IDENTIFIER:id ASSIGN expression:e SEMICOLON
        {:
            ExprResult expr = (ExprResult) e;

            if (!expr.type.equals(t)) {
                fatal_error(ErrorType.TYPE, "Esperado tipo '" + t + "', mas encontrado '" + expr.type + "'.");
            }

            declareVariable(id, t);
        :}
;

assignment ::= IDENTIFIER:id ASSIGN expression:e SEMICOLON
        {:
            SymbolInfo info = lookupVariable(id);
            ExprResult expr = (ExprResult) e;

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            } else if (!info.type.equals(expr.type)) {
                fatal_error(ErrorType.TYPE, "Esperado '" + info.type + "', encontrado '" + expr.type + "'");
            }
        :}
    | IDENTIFIER:id PLUS_ASSIGN expression:e SEMICOLON
        {:
            SymbolInfo info = lookupVariable(id);
            ExprResult expr = (ExprResult) e;

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            } else if (!expr.type.equals("number")) {
                fatal_error(ErrorType.TYPE, "Esperado 'number', encontrado '" + expr.type + "'");
            }
        :}
    | IDENTIFIER:id MINUS_ASSIGN expression:e SEMICOLON
        {:
            SymbolInfo info = lookupVariable(id);
            ExprResult expr = (ExprResult) e;

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            } else if (!expr.type.equals("number")) {
                fatal_error(ErrorType.TYPE, "Esperado 'number', encontrado '" + expr.type + "'");
            }
        :}
    | IDENTIFIER:id TIMES_ASSIGN expression:e SEMICOLON
        {:
            SymbolInfo info = lookupVariable(id);
            ExprResult expr = (ExprResult) e;

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            } else if (!expr.type.equals("number")) {
                fatal_error(ErrorType.TYPE, "Esperado 'number', encontrado '" + expr.type + "'");
            }
        :}
    | IDENTIFIER:id DIVIDE_ASSIGN expression:e SEMICOLON
        {:
            SymbolInfo info = lookupVariable(id);
            ExprResult expr = (ExprResult) e;

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            } else if (!expr.type.equals("number")) {
                fatal_error(ErrorType.TYPE, "Esperado 'number', encontrado '" + expr.type + "'");
            }
        :}
;

if_statement ::= IF LPAREN expression:e RPAREN block:b
        {:
            ExprResult expr = (ExprResult) e;
            if (!expr.type.equals("bool")) {
                fatal_error(ErrorType.TYPE, "Esperado 'bool', encontrado '" + expr.type + "'");
            }
        :}
    | IF LPAREN expression:e RPAREN block:b ELSE block:eb
        {:
            ExprResult expr = (ExprResult) e;
            if (!expr.type.equals("bool")) {
                fatal_error(ErrorType.TYPE, "Esperado 'bool', encontrado '" + expr.type + "'");
            }
        :}
    | IF LPAREN expression:e RPAREN block:b ELSE if_statement:ei
        {:
            ExprResult expr = (ExprResult) e;
            if (!expr.type.equals("bool")) {
                fatal_error(ErrorType.TYPE, "Esperado 'bool', encontrado '" + expr.type + "'");
            }
        :}
;

while_statement ::= WHILE LPAREN expression:e RPAREN block:b
    {:
        ExprResult expr = (ExprResult) e;
        if (!expr.type.equals("bool")) {
            fatal_error(ErrorType.TYPE, "Esperado 'bool', encontrado '" + expr.type + "'");
        }
    :}
;

for_statement ::=
        {:
            enterScope();

        :}
    FOR LPAREN declaration:d expression:e SEMICOLON assignment:as RPAREN unscoped_block:b
        {:
            ExprResult expr = (ExprResult) e;
            if (!expr.type.equals("bool")) {
                fatal_error(ErrorType.TYPE, "Esperado 'bool', encontrado '" + expr.type + "'");
            }

            exitScope();
        :}
;

function_statement ::= FUNCTION IDENTIFIER:id LPAREN RPAREN
        {:
            currentFunction = id;
            currentFunctionReturnType = "void";
            declareFunction(id, "void", new ArrayList<String>());
            enterScope();
        :}
        block
        {:
            exitScope();
            currentFunction = null;
            currentFunctionReturnType = null;
        :}
    | FUNCTION IDENTIFIER:id LPAREN parameter_list:p RPAREN
        {:
            ParameterList params = (ParameterList) p;
            currentFunction = id;
            currentFunctionReturnType = "void";
            declareFunction(id, "void", params.types);
            enterScope();
            for (int i = 0; i < params.names.size(); i++) {
                declareVariable(params.names.get(i), params.types.get(i));
            }
        :}
        block
        {:
            exitScope();
            currentFunction = null;
            currentFunctionReturnType = null;
        :}
    | FUNCTION type:t IDENTIFIER:id LPAREN RPAREN
        {:
            currentFunction = id;
            currentFunctionReturnType = (String) t;
            declareFunction(id, (String) t, new ArrayList<String>());
            enterScope();
        :}
        block
        {:
            exitScope();
            currentFunction = null;
            currentFunctionReturnType = null;
        :}
    | FUNCTION type:t IDENTIFIER:id LPAREN parameter_list:p RPAREN
        {:
            ParameterList params = (ParameterList) p;
            currentFunction = id;
            currentFunctionReturnType = (String) t;
            declareFunction(id, (String) t, params.types);
            enterScope();
            for (int i = 0; i < params.names.size(); i++) {
                declareVariable(params.names.get(i), params.types.get(i));
            }
        :}
        block
        {:
            exitScope();
            currentFunction = null;
            currentFunctionReturnType = null;
        :}
;

parameter_list ::= parameter:p
        {:
            RESULT = p;
        :}
    | parameter_list:pl COMMA parameter:p
        {:
            ParameterList paramList = (ParameterList) pl;
            ParameterList singleParam = (ParameterList) p;
            paramList.types.addAll(singleParam.types);
            paramList.names.addAll(singleParam.names);
            RESULT = paramList;
        :}
;

parameter ::= type:t IDENTIFIER:id
        {:
            ParameterList paramList = new ParameterList();
            paramList.addParameter((String) t, id);
            RESULT = paramList;
        :}
;

function_call ::= IDENTIFIER:id LPAREN RPAREN
        {:
            FunctionInfo func = lookupFunction(id);
            if (func == null) {
                fatal_error(ErrorType.SEMANTICAL, "Função '" + id + "' não declarada.");
            }
            if (!func.parameterTypes.isEmpty()) {
                fatal_error(ErrorType.TYPE, "Função '" + id + "' espera " + func.parameterTypes.size() + " argumentos, mas recebeu 0.");
            }
            RESULT = new ExprResult(func.returnType, id + "()");
        :}
    | IDENTIFIER:id LPAREN argument_list:args RPAREN
        {:
            FunctionInfo func = lookupFunction(id);
            if (func == null) {
                fatal_error(ErrorType.SEMANTICAL, "Função '" + id + "' não declarada.");
            }

            @SuppressWarnings("unchecked")
            List<ExprResult> arguments = (List<ExprResult>) args;

            if (arguments.size() != func.parameterTypes.size()) {
                fatal_error(ErrorType.TYPE, "Função '" + id + "' espera " + func.parameterTypes.size() + " argumentos, mas recebeu " + arguments.size() + ".");
            }

            for (int i = 0; i < arguments.size(); i++) {
                if (!arguments.get(i).type.equals(func.parameterTypes.get(i))) {
                    fatal_error(ErrorType.TYPE, "Argumento " + (i + 1) + " da função '" + id + "': esperado '" + func.parameterTypes.get(i) + "', encontrado '" + arguments.get(i).type + "'.");
                }
            }

            StringBuilder callStr = new StringBuilder(id + "(");
            for (int i = 0; i < arguments.size(); i++) {
                if (i > 0) callStr.append(", ");
                callStr.append(arguments.get(i).representation);
            }
            callStr.append(")");

            RESULT = new ExprResult(func.returnType, callStr.toString());
        :}
;

argument_list ::= expression:e
        {:
            List<ExprResult> args = new ArrayList<>();
            args.add((ExprResult) e);
            RESULT = args;
        :}
    | argument_list:al COMMA expression:e
        {:
            @SuppressWarnings("unchecked")
            List<ExprResult> args = (List<ExprResult>) al;
            args.add((ExprResult) e);
            RESULT = args;
        :}
;

return_statement ::= RETURN SEMICOLON
        {:
            if (currentFunction == null) {
                fatal_error(ErrorType.SEMANTICAL, "Comando 'return' fora de função.");
            }
            if (!currentFunctionReturnType.equals("void")) {
                fatal_error(ErrorType.TYPE, "Função '" + currentFunction + "' deve retornar um valor do tipo '" + currentFunctionReturnType + "'.");
            }
        :}
    | RETURN expression:e SEMICOLON
        {:
            if (currentFunction == null) {
                fatal_error(ErrorType.SEMANTICAL, "Comando 'return' fora de função.");
            }
            ExprResult expr = (ExprResult) e;
            if (!expr.type.equals(currentFunctionReturnType)) {
                fatal_error(ErrorType.TYPE, "Função '" + currentFunction + "' deve retornar '" + currentFunctionReturnType + "', mas encontrado '" + expr.type + "'.");
            }
        :}
;

block ::= LBRACE
        {:
            enterScope();
        :}
        statement_list:sl
        RBRACE
        {:
            exitScope();
        :}
    | LBRACE
        {:
            enterScope();
        :}
        RBRACE
        {:
            exitScope();
        :}
;

unscoped_block ::= LBRACE statement_list:sl RBRACE
    | LBRACE RBRACE
;

expression ::= logical_or_expr:e
    {: RESULT = e; :}
;

logical_or_expr ::= logical_or_expr:e1 OR logical_and_expr:e2
        {:
            ExprResult expr1 = (ExprResult) e1;
            ExprResult expr2 = (ExprResult) e2;
            if (!expr1.type.equals("bool") || !expr2.type.equals("bool")) {
                fatal_error(ErrorType.TYPE, "Operador '||' requer operandos do tipo 'bool'.");
            }

            RESULT = new ExprResult("bool", e1 + " || " + e2);
        :}
    | logical_and_expr:e
        {: RESULT = e; :}
;

logical_and_expr ::= logical_and_expr:e1 AND equality_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("bool") || !expr2.type.equals("bool")) {
            fatal_error(ErrorType.TYPE, "Operador '&&' requer operandos do tipo 'bool'.");
        }
        RESULT = new ExprResult("bool", e1 + " && " + e2);
    :}
  | equality_expr:e
    {: RESULT = e; :}
;

equality_expr ::= equality_expr:e1 EQ relational_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals(expr2.type)) {
            fatal_error(ErrorType.TYPE, "Comparação '==' entre tipos incompatíveis ('" + expr1.type + "' e '" + expr2.type + "').");
        }
        RESULT = new ExprResult("bool", e1 + " == " + e2);
    :}
  | equality_expr:e1 NE relational_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals(expr2.type)) {
            fatal_error(ErrorType.TYPE, "Comparação '!=' entre tipos incompatíveis ('" + expr1.type + "' e '" + expr2.type + "').");
        }
        RESULT = new ExprResult("bool", e1 + " != " + e2);
    :}
  | relational_expr:e
    {: RESULT = e; :}
;

relational_expr ::= relational_expr:e1 LT additive_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '<' requer operandos numéricos.");
        }
        RESULT = new ExprResult("bool", e1 + " < " + e2);
    :}
  | relational_expr:e1 LE additive_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '<=' requer operandos numéricos.");
        }
        RESULT = new ExprResult("bool", e1 + " <= " + e2);
    :}
  | relational_expr:e1 GT additive_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '>' requer operandos numéricos.");
        }
        RESULT = new ExprResult("bool", e1 + " > " + e2);
    :}
  | relational_expr:e1 GE additive_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '>=' requer operandos numéricos.");
        }
        RESULT = new ExprResult("bool", e1 + " >= " + e2);
    :}
  | additive_expr:e
    {: RESULT = e; :}
;

additive_expr ::= additive_expr:e1 PLUS multiplicative_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '+' requer operandos numéricos.");
        }
        RESULT = new ExprResult("number", e1 + " + " + e2);
    :}
  | additive_expr:e1 MINUS multiplicative_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '-' requer operandos numéricos.");
        }
        RESULT = new ExprResult("number", e1 + " - " + e2);
    :}
  | multiplicative_expr:e
    {: RESULT = e; :}
;

multiplicative_expr ::= multiplicative_expr:e1 TIMES unary_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '*' requer operandos numéricos.");
        }
        RESULT = new ExprResult("number", e1 + " * " + e2);
    :}
  | multiplicative_expr:e1 DIVIDE unary_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '/' requer operandos numéricos.");
        }
        RESULT = new ExprResult("number", e1 + " / " + e2);
    :}
  | multiplicative_expr:e1 MOD unary_expr:e2
    {:
        ExprResult expr1 = (ExprResult) e1;
        ExprResult expr2 = (ExprResult) e2;
        if (!expr1.type.equals("number") || !expr2.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '%' requer operandos numéricos.");
        }
        RESULT = new ExprResult("number", e1 + " % " + e2);
    :}
  | unary_expr:e
    {: RESULT = e; :}
;

unary_expr ::= NOT unary_expr:e
    {:
        ExprResult expr = (ExprResult) e;
        if (!expr.type.equals("bool")) {
            fatal_error(ErrorType.TYPE, "Operador '!' requer operando do tipo 'bool'.");
        }
        RESULT = new ExprResult("bool", "!" + e);
    :}
    | MINUS unary_expr:e
    {:
        ExprResult expr = (ExprResult) e;
        if (!expr.type.equals("number")) {
            fatal_error(ErrorType.TYPE, "Operador '-' unário requer operando numérico.");
        }
        RESULT = new ExprResult("number", "-" + e);
    :}
    | primary_expr:e                {: RESULT = e; :}
;

primary_expr ::= STRING_LITERAL:s   {: RESULT = new ExprResult("string", "\"" + s + "\""); :}
    | INT_LITERAL:i                 {: RESULT = new ExprResult("number", i.toString()); :}
    | FLOAT_LITERAL:f               {: RESULT = new ExprResult("number", f.toString()); :}
    | TRUE                          {: RESULT = new ExprResult("bool", "true"); :}
    | FALSE                         {: RESULT = new ExprResult("bool", "false"); :}
    | IDENTIFIER:id
        {:
            SymbolInfo info = lookupVariable(id);

            if (info == null) {
                fatal_error(ErrorType.SEMANTICAL, "Variável '" + id + "' não declarada.");
            }

            RESULT = new ExprResult((String) info.type, id);
        :}
    | function_call:fc              {: RESULT = fc; :}
    | LPAREN expression:e RPAREN    {: RESULT = new ExprResult((String) ((ExprResult) e).type, "(" + e + ")"); :}
;

type ::= INT_TYPE     {: RESULT = "number"; :}
       | FLOAT_TYPE   {: RESULT = "number"; :}
       | BOOL_TYPE    {: RESULT = "bool"; :}
       | STRING_TYPE  {: RESULT = "string"; :}
       | VOID         {: RESULT = "void"; :}
;